apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace 'xyz.realms.mgit'
    compileSdk 33
    defaultConfig {
        applicationId "xyz.realms.mgit"
        minSdk 31
        //noinspection ExpiredTargetSdkVersion
        targetSdk 31

        versionCode 254
        versionName "1.7.6"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }

    lint {
        abortOnError false
    }

    signingConfigs {
        if (project.hasProperty('special')) {
            release {
                keyAlias alias
                keyPassword password
                storeFile file(keystore)
                storePassword password
            }
        } else {
            release {
                keyAlias ''
                keyPassword ''
                storeFile file('/empty')
                storePassword ''
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        resources {
            excludes += ['**/plugin.properties']
        }
    }
}

configurations.configureEach {
    //  replace com.jcraft.jsch to com.github.mwiede:jsch
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == "com.jcraft" && details.requested.name == "jsch") {
            details.useTarget("com.github.mwiede:jsch:0.2.11")
        }
    }
    exclude module: 'httpclient'
}

//tasks.withType(JavaCompile).configureEach {
//    options.compilerArgs << "-Xlint:deprecation"
//}

dependencies {
    implementation 'androidx.fragment:fragment:1.6.2'
    implementation 'androidx.annotation:annotation:1.8.2'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.9.0'  // max

    // Jgit
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.10.0.202406032230-r'
    implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:6.10.0.202406032230-r'
    implementation 'org.eclipse.jgit:org.eclipse.jgit.lfs:6.10.0.202406032230-r'

    // ViewModel and LiveData
    runtimeOnly 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    runtimeOnly 'androidx.lifecycle:lifecycle-common:2.6.2' // max


    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.github.mwiede:jsch:0.2.11'  // max
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'com.scottyab:secure-preferences-lib:0.1.7'
    implementation 'org.conscrypt:conscrypt-android:2.5.3'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'

    //  Application Crash Reports
    implementation 'ch.acra:acra-mail:5.9.7'    // max
    implementation 'ch.acra:acra-dialog:5.9.7'  // max

    //  Debug and Test
    debugImplementation 'com.facebook.stetho:stetho:1.6.0'
    debugImplementation 'com.facebook.stetho:stetho-timber:1.6.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.13'
    testImplementation 'org.robolectric:shadows-support-v4:3.4-rc2'
}
